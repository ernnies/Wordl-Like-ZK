pragma circom 2.0.3;
include "../../../node_modules/circomlib/circuits/comparators.circom";
include "../../../node_modules/circomlib/circuits/gates.circom";
include "../../../node_modules/circomlib/circuits/poseidon.circom";

template SingleGuessCheck() {  
   //"Word of the day", private input
   signal input solution[5];
   signal input salt;
   //Current guess
   signal input guess[5];
   //Solution commitment
   signal input commitment;
   //Clue output (typically represented using colored squares â¬œðŸŸ©â¬œðŸŸ¨ðŸŸ¨)
   //"0" - the letter is absent (gray), "1" - the letter matches correctly (green)
   //"2" - the letter is present in solution but is located at a different position (yellow)
   signal output clue[5];
   
   signal correct[5];
   component eq[5];
   for (var i=0; i<5; i++) {
      eq[i] = IsEqual();
      eq[i].in[0] <== guess[i];
      eq[i].in[1] <== solution[i];
      correct[i] <== eq[i].out;
   }

   var i=0;
   var j=0;
   //Unfortunately, Circom (as of v2.0.3) does not allow dynamic component declaration inside loops.
   //It leads to the following error: error[T2011]: Signal or component declaration outside initial scope.
   //It means that we have to declare how many instances of a component we would like to have in advance.
   component match[5][5];
   component alreadyPresent[5][5];
   component wasOrIsTaken[5][5];
   component matchAndNotCorrect[5][5];
   component matchAndNotTaken[5][5];
   component andNotYetPresent[5][5];
   component present[5][5];
   signal solutionCharsTaken[5][5];
   for (i=0; i<5; i++) { // guess index
      for (j=0; j<5; j++) { // solution index
         //True if the i-th guess letter is the same as j-th solution letter.
         match[i][j] = IsEqual();
         match[i][j].in[0] <== guess[i];
         match[i][j].in[1] <== solution[j];
         //True if guess letter has a match but it didn't match with
         //solution letter exactly in this (i-th) position.
         //Let's call it an "elsewhere match".
         matchAndNotCorrect[i][j] = AndNotB();
         matchAndNotCorrect[i][j].a <== match[i][j].out;
         matchAndNotCorrect[i][j].b <== correct[i];
         //True if there is an elsewhere match but the matching letter of the solution hasn't been taken
         //Let's call it a vacant elsewhere match.
         matchAndNotTaken[i][j] = AndNotB();
         matchAndNotTaken[i][j].a <== matchAndNotCorrect[i][j].out;
         if (i==0){
            matchAndNotTaken[i][j].b <== correct[j];
         } else {
            matchAndNotTaken[i][j].b <== solutionCharsTaken[i-1][j];
         }
         
         //True if there has been at least one vacant elsewhere match with any previous solution letter
         alreadyPresent[i][j] = OR();
         if (j==0){
           alreadyPresent[i][j].a <== 0;
         } else {
           alreadyPresent[i][j].a <== alreadyPresent[i][j-1].out;
         }        
         alreadyPresent[i][j].b <== matchAndNotTaken[i][j].out;
         
         //Marking "present" only once (only for the first occurence).
         //True if it is a vacant match but it hasn't happened for any previous solution letter
         andNotYetPresent[i][j] = AndNotB();
         andNotYetPresent[i][j].a <== matchAndNotTaken[i][j].out;
         if (j==0){
            andNotYetPresent[i][j].b <== 0;   
         } else {
            andNotYetPresent[i][j].b <== alreadyPresent[i][j-1].out;
         }         

         //"Flattening" the 2d array by carrying the previous "presents" over to the top layer 
         present[i][j] = OR();
         if (j==0){
           present[i][j].a <== 0;
         } else {
           present[i][j].a <== present[i][j-1].out;
         }        
         present[i][j].b <== andNotYetPresent[i][j].out;

         //Merging the previously taken solution letters with the newly taken ones 
         wasOrIsTaken[i][j] = OR();
         wasOrIsTaken[i][j].a <== andNotYetPresent[i][j].out;
         if (i==0){
            wasOrIsTaken[i][j].b <== correct[j];
         } else {
            wasOrIsTaken[i][j].b <== solutionCharsTaken[i-1][j];
         }
         solutionCharsTaken[i][j] <== wasOrIsTaken[i][j].out;
      }
      //"0" if the letter is absent, "1" if it's an exact match 
      //and "2" if it's present elsewhere in the solution.
      clue[i] <== correct[i] + present[i][4].out * 2;
   }

   /*
   * Converting the ASCII solution to a single number.
   * Alternatively, we could make a Poseidon hasher with 6 inputs,
   * but it would almost double the circuit constraint number.
   * We are confident that none of the chars has a code above 99,
   * so we can safely "stick" them together as two-digit decimals.
   */
   signal solutionAsNumber[5];
   solutionAsNumber[0] <== solution[0];
   for (i=1; i<5; i++){
      solutionAsNumber[i] <== solutionAsNumber[i-1] + solution[i] * (100 ** i);
   }
   //Hashing the solution
   component solutionHash = Poseidon(2);
   solutionHash.inputs[0] <== solutionAsNumber[4];
   solutionHash.inputs[1] <== salt;
   //Constrain the hash to a publicly committed one
   commitment === solutionHash.out;
}

//Convenience component that inverts the "b" input 
//and performs && of the result with the "a" input
template AndNotB(){
   signal input a;
   signal input b;
   signal output out;

   component not = NOT();
   not.in <== b;

   component and = AND();
   and.a <== a;
   and.b <== not.out;
   out <== and.out;
}

//For ZK-REPL (https://zkrepl.dev/)
/* INPUT = {
     "solution": [ 83, 84, 69, 69, 76 ],
  "salt": 362986289847779600,
  "guess": [ 83, 84, 65, 82, 84 ],
  "commitment": "15057754752634756475908235894514270422456734783907164695964318985994495471810"
} */